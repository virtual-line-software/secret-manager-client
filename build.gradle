import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
	id 'java-library'
	id 'maven-publish'
	id 'org.springframework.boot' version '3.2.5' apply false
	id 'io.spring.dependency-management' version '1.1.4'
}

group = 'software.virtualline'
version = '1.0.0'

java {
	sourceCompatibility = '21'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

tasks.withType(JavaCompile).configureEach {
	options.compilerArgs << '-parameters'
}

repositories {
	mavenCentral()
}

dependencyManagement {
	imports {
		mavenBom SpringBootPlugin.BOM_COORDINATES
	}
}

dependencies {
	api 'org.springframework.security:spring-security-oauth2-client'
	api 'org.springframework.boot:spring-boot-starter-json'
	api 'org.springframework:spring-web'
	api 'org.springframework:spring-webflux'

	implementation "com.google.cloud:google-cloud-secretmanager:2.73.0"

	implementation 'org.projectlombok:lombok:1.18.30'
	annotationProcessor 'org.projectlombok:lombok:1.18.30'
}

publishing {
	repositories {
		maven {
			ext.extProgram = new Properties()
			extProgram.load(file(System.getProperty("user.home") + "/.vls/credentials").newDataInputStream())
			name = "GitHubPackages"
			url = uri("https://maven.pkg.github.com/virtual-line-software/secret-manager-client")
			credentials {
				username = extProgram['github_user'] ?: System.getenv("USERNAME")
				password = extProgram['github_token'] ?: System.getenv("TOKEN")
			}
		}
	}
	publications {
		gpr(MavenPublication) {
			from(components.java)
			artifactId = 'secret-manager-client'
		}
	}
}

tasks.named('test') {
	useJUnitPlatform()
}
